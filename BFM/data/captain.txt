




# Captain's decision problem
# Russell Almold, Graphical Belief Modeling, Chapman & Hall 1995
# Variable A denotes Arrival delay
# Variable D denotes Departure delay
# Variable S denotes Sailing delay
# Variable L denotes Loading delay
# Variable F denotes Forecast of weather
# Variable M denotes Maintenaince performed
# Variable W denotes Weather 
# Variable R denotes Repair made
#BFM_position (280,30)
DEFINE VARIABLE A { 0 1 2 3 4 5 6 };
#BFM_position (160,250)
DEFINE VARIABLE D { 0 1 2 3 };
#BFM_position (310,150)
DEFINE VARIABLE S { 0 1 2 3 };
#BFM_position (330,260)
DEFINE VARIABLE L { t f };
#BFM_position (120,320)
DEFINE VARIABLE F { t f };
#BFM_position (510,130)
DEFINE VARIABLE M { t f };
#BFM_position (480,410)
DEFINE VARIABLE W { t f };
#BFM_position (580,150)
DEFINE VARIABLE R { t f };
# The arrival delay is sum of departure and sailing delays
#BFM_position (260,120)
DEFINE RELATION ADS { A D S };
SET VALUATION ADS { ( 3 3 0 ) ( 5 2 3 ) ( 5 3 2 ) ( 3 2 1 ) ( 1 1 0 ) ( 3 1 2 ) ( 3 0 3 ) ( 1 0 1 ) ( 4 2 2 ) ( 2 2 0 ) ( 6 3 3 ) ( 4 3 1 ) ( 2 1 1 ) ( 4 1 3 ) ( 2 0 2 ) ( 0 0 0 ) } 1.0;

# Loading delay, foul weather forecast and maintenance if true adds one day each to the
# departure delay
#BFM_position (380,330)
DEFINE RELATION DLMF { D L M F };
SET VALUATION DLMF { ( 1 f f t ) ( 1 f t f ) ( 1 t f f ) ( 3 t t t ) ( 0 f f f ) ( 2 f t t ) ( 2 t f t ) ( 2 t t f ) } 1.0;

# Heavy weather and repair at sea adds 1 day to the sailing delay.
# This proposition is true about 90% of the time
#BFM_position (470,280)
DEFINE RELATION SWR { S W R };
SET VALUATION SWR { ( 2 t t ) ( 1 f t ) ( 1 t f ) ( 0 f f ) } 0.9;

# The weatherman is reliable about 80% of the time
#BFM_position (270,400)
DEFINE RELATION WF { W F };
SET VALUATION WF { ( f f ) ( t t ) } 0.8;

# Given the ship has undergone a maintenance, the chance it needs a repair 
# at sea is between 10% to 30%
#BFM_position (660,90)
DEFINE CONDITIONAL RELATION RM1 { R } GIVEN { M };
SET CONDITIONAL VALUATION RM1 GIVEN {t } { ( t ) } 0.1, { ( f ) } 0.7;

# Given the ship has not undergone a maintenance, the chance it needs a repair 
# at sea is between 20% to 70%
#BFM_position (600,60)
DEFINE CONDITIONAL RELATION RM2 { R } GIVEN { M };
SET CONDITIONAL VALUATION RM2 GIVEN {f } { ( f ) } 0.2, { ( t ) } 0.2;

# Forecast predicts foul weather with chance .2, fair weather with chance .6
#BFM_position (20,260)
DEFINE RELATION PRIORF { F };
SET VALUATION PRIORF { ( f ) } 0.6, { ( t ) } 0.2;

# Loading is delayed with chance .3 and on schedule with chance .5
#BFM_position (260,200)
DEFINE RELATION PRIORL { L };
SET VALUATION PRIORL { ( f ) } 0.5, { ( t ) } 0.3;

# The ship is not maintained
#BFM_position (470,40)
DEFINE RELATION PRIORM { M };
SET VALUATION PRIORM { ( f ) } 1.0;